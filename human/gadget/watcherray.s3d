#ifndef WATCHERRAY_H
#define WATCHERRAY_H

#include "../../scene/interactionray.s3d"
#include "../../scene/behavior/mono.s3d"
#include "humangadget.s3d"

class BHWatcherRay : Mono
{
	var m_elapsedTime;
	
	onCollision(object);
	onCollisionEnter(object);
	onCollisionExit(object);
};

function BHWatcherRay::BHWatcherRay() {
	m_elapsedTime = 0;
}

function BHWatcherRay::onCollision(object) {
	if (GetTime() - m_elapsedTime > 1500) {
		object.getBehavior().onWatch();	
	}
}

function BHWatcherRay::onCollisionEnter(object) {
	m_elapsedTime = GetTime();
}

function BHWatcherRay::onCollisionExit(object) {
	object.getBehavior().onUnWatch();
	m_elapsedTime = 0;
}

class WatcherRay : HumanGadget
{
	var m_aim;
	var m_ray;
	
	applyTo(world);
	init();
};

function WatcherRay::WatcherRay(human)
{
	HumanGadget(human);
	
	var head = human.getPart(HEAD_ID).getCVmObj();
	
	var meshAim = CVmNewMesh(VRP_CIRCLE, 20, 0.0002, 0);
	meshAim.Rotate(90, [1,0,0]);
	
	m_aim = CVmObj();
	m_aim.LinkToMesh(meshAim);
	
	head.AddChild(m_aim);
	
	m_ray = InteractionRay(m_aim);
	m_ray.linkToBehavior(BHWatcherRay());
}

function WatcherRay::applyTo(world) {
	world.addInteractionRay(m_ray);
	m_human.applyTo(world);
}

function WatcherRay::init() {
	// Set the offset from the human's face and the ray
	m_aim.SetPosition([0, 0, 0.11]);
	// Set the maximum distance which the ray can reaching the target
	m_ray.setLenght(300);
	
	m_human.init();
}

#endif // WATCHERRAY_H
